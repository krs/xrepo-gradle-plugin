plugins {
  id 'java-gradle-plugin'
  id 'eclipse'
  id 'jacoco'
  id 'com.gradle.plugin-publish' version '0.10.0'
  id 'com.github.kt3k.coveralls' version '2.6.3'	
}
group 'com.github.krs.xrepo-gradle-plugin'
version '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

repositories {
    mavenCentral()
}

project.ext {
	junitVersion = '5.3.1'
	junitPlatformVersion = '1.3.1'
}

dependencies {
    compile gradleApi()
    
    testCompile gradleTestKit()
    testCompile (
        'org.assertj:assertj-core:3.11.1',
        'commons-io:commons-io:2.6',
        "org.junit.jupiter:junit-jupiter-api:$junitVersion",
        "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
        "org.junit.vintage:junit-vintage-engine:$junitVersion",
        "org.junit.platform:junit-platform-launcher:$junitPlatformVersion",
        "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
    )
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'junit-vintage'
    }
}

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
               TestLogEvent.PASSED,
               TestLogEvent.SKIPPED,
               TestLogEvent.STANDARD_ERROR,
               TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showExceptions true
        showStackTraces true
    }
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

pluginBundle {
  website = 'https://github.com/krs/xrepo-gradle-plugin'
  vcsUrl = 'https://github.com/krs/xrepo-gradle-plugin'
  description = "Cross-repository Gradle plugin modifies dependency resolution to use artifacts built from the same branch even if they're in different repository."
  tags = ['git', 'versioning', 'branches']

  plugins {
    xrepo {
      id = 'com.github.krs.xrepo'
      displayName = 'Cross-repository Gradle plugin'
    }
  }
}